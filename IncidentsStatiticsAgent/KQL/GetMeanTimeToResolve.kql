// GetMeanTimeToResolve
// Calculates the Mean Time to Resolve (MTTR) for incidents closed within the specified period and compares it to the MTTR for the entire previous month.
// MTTR is defined as the average time (in minutes) between incident creation and closure.
// Returns: 
// - MTTR for the specified period
// - MTTR for the previous month
// - Exact start and end dates of the two time windows used for the calculation.
let start_time = datetime({start_date}); //E.g.: ago(24h); datetime(2025-11-01T00:00:00Z);
let end_time = datetime({end_date}); //E.g. now(); 
let lastMonthStart = startofmonth(datetime_add('month', -1, now()));
let lastMonthEnd = endofmonth(datetime_add('month', -1, now()));
let timeWindowMTTR = SecurityIncident
| summarize arg_max(LastModifiedTime, *) by IncidentName
| extend alertsCount = toint(parse_json((AdditionalData).alertsCount))
| where Status =~ "Closed" and alertsCount > 0
| where ClosedTime between (start_time .. end_time)
| extend ResolveTime = datetime_diff('minute', ClosedTime, CreatedTime)
| summarize MTTR = round(avg(ResolveTime),0)
| extend j = "forcedjoin"
| project timeWindowMTTR = MTTR, j;
let lastMonthMTTR = SecurityIncident
| summarize arg_max(LastModifiedTime, *) by IncidentName
| extend alertsCount = toint(parse_json((AdditionalData).alertsCount))
| where Status =~ "Closed" and alertsCount > 0
| where ClosedTime between (lastMonthStart .. lastMonthEnd)
| extend ResolveTime = datetime_diff('minute', ClosedTime, CreatedTime)
| summarize MTTR = round(avg(ResolveTime),0)
| extend j = "forcedjoin"
| project lastMonthMTTR = MTTR, j;
timeWindowMTTR
| join lastMonthMTTR on j
| extend PercentageChange = round(((timeWindowMTTR - lastMonthMTTR) * 100.0 / lastMonthMTTR), 0)
| project MTTRinMin = timeWindowMTTR, LastMonthMTTRinMin = lastMonthMTTR, PercentageChange, start_time, end_time, lastMonthStart, lastMonthEnd
