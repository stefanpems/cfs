// GetTopIncidentsOwners
// Retrieves the list of top owners for incidents created within the specified period.
// The summary can optionally be filtered by severity, status, and classification. Valid values:
// - severities: "High", "Medium", "Low", "Informational" (use empty array as default)
// - status: "New", "Active", "Closed" (use empty array as default)
// - classificationResults: "truePositive", "falsePositive", "BenignPositive", "Undetermined", "" (use empty array as default)
let start_time = datetime({start_date}); //E.g.: ago(24h); datetime(2025-11-01T00:00:00Z);
let end_time = datetime({end_date}); //E.g. now(); 
let severities = dynamic([{csv_of_severities}]);  //E.g.: dynamic(["High","Medium","Low","Informational"]); dynamic([]);
let status = dynamic([{csv_of_status}]);  //E.g.: dynamic(["New","Active","Closed"]); dynamic([]);
let classificationResults = dynamic([{csv_of_classifications}]);  //E.g.: dynamic(["truePositive","falsePositive","BenignPositive","Undetermined"]); dynamic([]);
let top_owners_number = int({top_owners_number}); //E.g.: int(10)
SecurityIncident
| summarize arg_max(LastModifiedTime, *) by IncidentName
| extend alertsCount = toint(parse_json((AdditionalData).alertsCount))
| where alertsCount > 0 
| where CreatedTime between (start_time .. end_time)
| where coalesce(array_length(severities), 0) == 0 
  or Severity in~ (severities)
| where coalesce(array_length(status), 0) == 0 
  or Status in~ (status)
| where coalesce(array_length(classificationResults), 0) == 0 
  or Classification in~ (classificationResults)
| extend OwnerUPN = tostring(parse_json(Owner).userPrincipalName)
| summarize IncidentCount = count() by OwnerUPN
| top top_owners_number by IncidentCount desc
| project OwnerUPN, IncidentCount
| order by IncidentCount desc, OwnerUPN asc
