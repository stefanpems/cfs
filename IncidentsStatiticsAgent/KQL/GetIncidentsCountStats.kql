// GetIncidentsCountStats
// Provides a summary of incidents for the selected time range, grouped by severity and title and including:
// - Total count
// - Status breakdown (New, Active, Closed)
// - Classification breakdown (BenignPositive, FalsePositive, TruePositive, Undetermined)
// - MITRE ATT&CK tactics and techniques 
// The summary can optionally be filtered by severity, status, and classification. Valid values:
// - severities: "High", "Medium", "Low", "Informational" (use empty array as default)
// - status: "New", "Active", "Closed" (use empty array as default)
// - classificationResults: "truePositive", "falsePositive", "BenignPositive", "Undetermined", "" (use empty array as default)
// Results are sorted by Total count (descending), Severity (descending), and Title (ascending).
let start_time = datetime({start_date}); //E.g.: ago(24h); datetime(2025-11-01T00:00:00Z);
let end_time = datetime({end_date}); //E.g. now(); 
let severities = dynamic([{csv_of_severities}]);  //E.g.: dynamic(["High","Medium","Low","Informational"]); dynamic([]);
let status = dynamic([{csv_of_status}]);  //E.g.: dynamic(["New","Active","Closed"]); dynamic([]);
let classificationResults = dynamic([{csv_of_classifications}]);  //E.g.: dynamic(["truePositive","falsePositive","BenignPositive","Undetermined"]); dynamic([]);
let top_incs_number = int({top_results_number}); //E.g.: int(10)
let real_incidents = SecurityIncident
| summarize arg_max(LastModifiedTime, *) by IncidentName
| where TimeGenerated between (start_time .. end_time) 
| extend alertsCount = toint(parse_json((AdditionalData).alertsCount))
| where alertsCount > 0 
| where coalesce(array_length(severities), 0) == 0 
  or Severity in~ (severities)
| where coalesce(array_length(status), 0) == 0 
  or Status in~ (status)
| where coalesce(array_length(classificationResults), 0) == 0 
  or Classification in~ (classificationResults)
| extend SevNum =
    case(
        Severity == "Informational", 1,
        Severity == "Low",           2,
        Severity == "Medium",        3,
        Severity == "High",          4,
        Severity == "Critical",      5,
        0
    )
| extend IncidentSeverityAndTitle = strcat("[",Severity,"] ",Title);
let top_incidents = real_incidents
| summarize TotalCount = count() by IncidentSeverityAndTitle, Severity, SevNum, Title;
let classified_incidents = real_incidents
| summarize ClosedCount = countif(Status == "Closed"),
            ActiveCount = countif(Status == "Active"),
            NewCount = countif(Status == "New"),
            BenignPositiveCount = countif(Classification == "BenignPositive"),
            FalsePositiveCount = countif(Classification == "FalsePositive"),
            TruePositiveCount = countif(Classification == "TruePositive"),
            UndeterminedCount = countif(Classification == "Undetermined") by IncidentSeverityAndTitle;
let ttIncidents = real_incidents
| extend AdditionalDataParsed = parse_json(AdditionalData)
| summarize Occurrences=count(), Tactics=make_set(AdditionalDataParsed.tactics), Techniques=make_set(AdditionalDataParsed.techniques) by IncidentSeverityAndTitle, Severity, SevNum, Title, Status
| order by Occurrences desc, SevNum desc, Title asc     
| project IncidentSeverityAndTitle, Severity, SevNum, Title, Occurrences, Status, Tactics, Techniques;
top_incidents
| join kind=leftouter (classified_incidents) on IncidentSeverityAndTitle
| join kind=leftouter (ttIncidents) on IncidentSeverityAndTitle      
| order by TotalCount desc, SevNum desc, Title asc         
| top top_incs_number by TotalCount
| project IncidentSeverityAndTitle, Severity, SevNum, Title, TotalCount, NewCount, ActiveCount, ClosedCount, BenignPositiveCount, FalsePositiveCount, TruePositiveCount, UndeterminedCount, Tactics, Techniques
